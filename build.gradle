buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.4'
    }
}

apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'idea'

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-deprecation:false"]
}

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.oc.version}"
group = "li.cil.oc"
archivesBaseName = "OpenComputers"

def getGitRef() {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    return stdout.toString().trim()
  } catch (Throwable e) {
    return "unknown"
  }
}

if (System.getenv("PROMOTED_NUMBER") != null)
    version += ".${System.getenv("PROMOTED_NUMBER")}"
else if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"
else
    version += "+" + getGitRef()

ext.simpleVersion = version
version = "MC${config.minecraft.version}-${project.version}"

runClient {
    jvmArgs '-Dfml.coreMods.load=li.cil.oc.common.launch.TransformerLoader'
}
runServer {
    jvmArgs '-Dfml.coreMods.load=li.cil.oc.common.launch.TransformerLoader'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

repositories {
    maven {
        name = "mightypirates"
        url = "https://maven.cil.li/"
    }
    // These are necessary because some parts of the maven repo is weirdly structured, this needs to be fixed.
    ivy {
        name 'weird maven repos'
        artifactPattern "https://maven.cil.li/[module]/[revision]/[module]-[revision].[ext]"
    }
    ivy {
        name 'weird maven repos 2'
        artifactPattern "https://maven.cil.li/[module]/[revision]/[module]-[revision]-[classifier].[ext]"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name = "ic2"
        url = "https://maven.ic2.player.to/"
    }
    maven {
        name = "gt"
        url = "https://gregtech.overminddl1.com/"
    }
	ivy {
        name 'gtnh_download_source'
        artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]"
    }
    maven { // GalacticGreg, YAMCore,..
        name 'UsrvDE'
        url "http://jenkins.usrv.eu:8081/nexus/content/repositories/releases/"
    }
	maven {
		url "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/"
	}
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "curse.maven"
        url = "https://cursemaven.com"
    }
}

configurations {
    embedded
    implementation.extendsFrom embedded
}

dependencies {
    compileOnly "appeng:RotaryCraft:${config.rotc.version}:api"
    compileOnly "com.github.GTNewHorizons:Applied-Energistics-2-Unofficial:master-SNAPSHOT:dev"
    compileOnly "com.github.GTNewHorizons:NotEnoughItems:master-SNAPSHOT:dev"
    compileOnly ("com.github.GTNewHorizons:EnderStorage:master-SNAPSHOT:dev") {
        transitive = false
    }
    compileOnly "com.github.GTNewHorizons:Waila:master-SNAPSHOT:dev"
    compileOnly "com.github.GTNewHorizons:ForgeMultipart:master-SNAPSHOT:dev"
    compileOnly "codechicken:WR-CBE:${config.minecraft.version}-${config.wrcbe.version}:dev"
    compileOnly "com.bluepowermod:BluePower:${config.bluepower.version}:deobf"
    compileOnly "com.gregoriust.gregtech:gregtech:${config.gt.version}:dev"
    compileOnly "igwmod:IGW-Mod-1.7.10:${config.igwmod.version}:userdev"
    compileOnly "li.cil.tis3d:TIS-3D:${config.tis3d.version}:dev"
    compileOnly "net.industrial-craft:industrialcraft-2:${config.ic2.version}:dev"
    compileOnly "net.sengir.forestry:forestry_${config.minecraft.version}:${config.forestry.version}:dev"
    compileOnly "dev.modwarriors.notenoughkeys:NotEnoughKeys:${config.minecraft.version}-${config.nek.version}:deobf-dev"
    compileOnly "qmunity:QmunityLib:${config.qmunitylib.version}:deobf"
    compileOnly "tmech:TMechworks:${config.minecraft.version}-${config.tmech.version}:deobf"
    compileOnly ("mrtjp:ProjectRed:${config.projred.version}:dev") {
        exclude module: 'CoFHCore'
    }
    // compileOnly "coloredlightscore:ColoredLightsCore:${config.coloredlights.version}:api"

    compileOnly name: 'buildcraft', version: config.bc.version, classifier: "dev", ext: 'jar'
    compileOnly name: 'GalacticraftCoreAll', version: config.gc.version, ext: 'jar'
    compileOnly name: 'MekanismAll', version: config.mekanism.version, ext: 'jar'
    compileOnly name: 'redlogic', version: config.redlogic.version, ext: 'jar'

    compileOnly name: 'CoFHLib', version: config.cofhlib.version, ext: 'jar'
    compileOnly name: 'CoFHCore', version: config.cofhcore.version, ext: 'jar'
    compileOnly name: 'MineFactoryReloaded', version: config.mfr.version, ext: 'jar'
    compileOnly name: 'ComputerCraft', version: config.cc.version, ext: 'jar'
    compileOnly name: 'EnderIO', version: config.eio.version, ext: 'jar'
    compileOnly name: 'Railcraft', version: config.rc.version, ext: 'jar'
    compileOnly name: 'BloodMagic', version: config.bloodmagic.version, ext: 'jar'
    compileOnly name: 'ExtraCells', version: config.ec.version, ext: 'jar'
	compileOnly name: 'ThaumicEnergistics', version: config.thaumicenergistics.version, ext: 'jar'
    compileOnly "thaumcraft:Thaumcraft:1.7.10-4.2.3.5:dev"

    compile 'com.google.code.findbugs:jsr305:1.3.9' // Annotations used by google libs.

    embedded files('libs/OpenComputers-JNLua.jar', 'libs/OpenComputers-LuaJ.jar')

    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.scalactic:scalactic_2.11:2.2.6"
    testCompile "org.scalatest:scalatest_2.11:2.2.6"
}

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"

    replace "@VERSION@", project.simpleVersion
    replace "/*@MCVERSIONDEP@*/", ", acceptedMinecraftVersions = \"[${config.minecraft.version},${config.minecraft.version}+)\""
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.simpleVersion, 'mcversion': config.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'application.conf'
        filter { line ->
            line.replaceAll("@VERSION@", project.simpleVersion)
        }
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'application.conf'
    }

    // Move access transformer to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
    attributes FMLCorePlugin: "li.cil.oc.common.launch.TransformerLoader"
    attributes FMLCorePluginContainsFMLMod: "true"
    attributes FMLAT: "oc_at.cfg"
}

jar {
    exclude "cofh/**"
    exclude "mods/**"
    configurations.embedded.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    classifier = 'universal'
    manifest commonManifest
}

javadoc {
    include 'li/cil/oc/api/**'
}

// because the normal default jar task has been modified to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    exclude "cofh/**"
    exclude "mods/**"
    configurations.embedded.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    classifier = 'dev'
    manifest commonManifest
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'li/cil/oc/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.scala
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives apiJar
    archives javadocJar
    archives sourcesJar
}

// this is needed for IntelliJ so we don't have to copy over the assets manually every time
idea {
    module {
        outputDir = file('build/classes/main')
    }
}
